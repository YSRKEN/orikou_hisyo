/* 艦隊情報に関するイベント処理 */

//*艦隊情報画面を表示
*show_fleetinfo_window
	gsel kFleetInfoWindowID, 2
	show_window(kFleetInfoWindowID) = TRUE
return

//*艦隊情報を計算して出力する
*get_fleetinfo
	// 情報読み取り
	master_level = GetMasterLevel()		;司令部レベル
	target_unit_count = unit_count(fleetinfo_number_index)	;艦隊の艦娘数
	dim target_unit_id, target_unit_count	;艦娘のID
	dim target_unit_level, target_unit_count	;艦娘の練度
	dim target_unit_search, target_unit_count	;艦娘の索敵値
	dim target_weapon_id, target_unit_count, kWeapons	;装備のID
	dim target_weapon_airs, target_unit_count, kWeapons	;装備の搭載数
	dim target_weapon_level, target_unit_count, kWeapons	;装備の改修度・熟練度
	dim target_weapon_count, target_unit_count	;所持装備数
	for n, 0, target_unit_count
		// 艦娘情報
		;艦娘のマークを読み取り、判定を行う
		gsel GetUnitID(fleetinfo_number_index, n)
		sdim kammusu_mark_temp, kKammusuMarkLen
		for k, 0, kKammusuMarkLen
			pget 339 + k, 122
			if(ginfo_r < 210) :kammusu_mark_temp += "b" :else :kammusu_mark_temp += "w"
		next
		target_unit_id(n) = -1
		for k, 0, kammusu_data_size
			if(kammusu_mark(k) == kammusu_mark_temp){
				target_unit_id(n) = k
				_break
			}
		next
		;艦娘の練度を取得する
		gsel GetUnitID(fleetinfo_number_index, n)
		target_unit_level(n) = 1
		dim level_num, 3
		ReadNumber level_num, 3, unit_level_pos_x, 112, 10, 14, 170, FALSE
		if(level_num(1) == 10){
			target_unit_level(n) = limit(level_num(2), 1, 9)
		}else{
			if(level_num(0) == 10){
				target_unit_level(n) = limit(level_num(1), 1, 9) * 10 + limit(level_num(2), 0, 9)
			}else{
				target_unit_level(n) = 100 + limit(level_num(1), 0, 5) * 10 + limit(level_num(2), 0, 9)
			}
		}
		;索敵値を読み取る
		gsel GetUnitID(fleetinfo_number_index, n)
		dim search_num, 3
		ReadNumber search_num, 3, search_pos_x, 423, 7, 12, 170, FALSE
		target_unit_search(n) = limit(search_num(2), 0, 9)
		if(search_num(1) != 10) :target_unit_search(n) += limit(search_num(1), 1, 9) * 10
		if(search_num(0) != 10) :target_unit_search(n) += limit(search_num(0), 1, 9) * 100
//		if(n == 4) :dialog "" + target_unit_search(n)
		// 装備情報
		for w, 0, kWeapons
			target_weapon_id(n, w) = -1
			gsel GetUnitID(fleetinfo_number_index, n)
			;装備欄なし、および未装備の場合はそれ以降読み込まない
			if(NearColor(546, 174 + 33 * w, 27, 173, 176) == FALSE){
				_break
			}
			if(NearColor(362, 175 + 33 * w, 39, 64, 46) && NearColor(361, 173 + 33 * w, 41, 63, 46)){
				_break
			}
			;装備のマークを読み取り、判定を行う
			sdim weapon_mark_temp, kWeaponMarkLen
			for k, 0, kWeaponMarkLen
				pget 379 + k, 176 + 33 * w
				if(ginfo_r < 210) :weapon_mark_temp += "b" :else :weapon_mark_temp += "w"
			next
			for k, 0, weapon_data_size
				if(weapon_mark(k) == weapon_mark_temp){
					target_weapon_id(n, w) = k
					_break
				}
			next
			;搭載数を読み取る
			if(target_weapon_id(n, w) != -1){
				switch(weapon_type(target_weapon_id(n, w)))
				case "オートジャイロ"
				case "艦上攻撃機"
				case "艦上戦闘機"
				case "艦上偵察機"
				case "艦上爆撃機"
				case "水上偵察機"
				case "水上爆撃機"
				case "多用途水上機/水上戦闘機"
				case "対潜哨戒機"
				case "大型飛行艇"
					dim airs_num, 2
					ReadNumber airs_num, 2, airs_pos_x, 169 + w * 33, 8, 12, 200, FALSE
					if(airs_num(1) != 10){
						target_weapon_airs(n, w) = limit(airs_num(1), 0, 9)
						if(airs_num(0) != 10) :target_weapon_airs(n, w) += limit(airs_num(0), 1, 9) * 10
					}
				swend
			}
			;装備数を更新する
			target_weapon_count(n)++
		next
	next
	;装備から、索敵値に補正を入れる
	for n, 0, target_unit_count
		for w, 0, target_weapon_count(n)
			if(target_weapon_id(n, w) == -1) :_continue
			target_unit_search(n) -= weapon_search(target_weapon_id(n, w))
		next
	next
	;艦載機熟練度を読み取る
	for n, 0, target_unit_count
		gsel GetUnitID(fleetinfo_number_index, n)
		for w, 0, target_weapon_count(n)
			id = target_weapon_id(n, w)
			if(id == -1) :_continue
			switch(weapon_type(id))
			case "艦上攻撃機"
			case "艦上戦闘機"
			case "艦上爆撃機"
			case "艦上偵察機"
			case "水上爆撃機"
			case "水上偵察機"
			case "多用途水上機/水上戦闘機"
				// Max(>>)
				if(NearColor(508, 176 + 33 * w, 214, 159, 21)){
					if(NearColor(502, 176 + 33 * w, 214, 159, 20)){
						target_weapon_level(n, w) = 7
						swbreak
					}
				}
				// Level 4・5・6(/・//・///)
				if(NearColor(507, 176 + 33 * w, 213, 156, 8)){
					if(NearColor(503, 176 + 33 * w, 213, 156, 8)){
						if(NearColor(499, 176 + 33 * w, 213, 156, 8)){
							target_weapon_level(n, w) = 6
						}else{
							target_weapon_level(n, w) = 5
						}
					}else{
						target_weapon_level(n, w) = 4
					}
					swbreak
				}
				// Level 1・2・3(|・||・|||)
				if(NearColor(508, 176 + 33 * w, 154, 181, 208)){
					if(NearColor(504, 176 + 33 * w, 154, 181, 208)){
						if(NearColor(500, 176 + 33 * w, 154, 181, 208)){
							target_weapon_level(n, w) = 3
						}else{
							target_weapon_level(n, w) = 2
						}
					}else{
						target_weapon_level(n, w) = 1
					}
					swbreak
				}
				swbreak
			swend
		next
	next
	;制空値を計算する(艦載機熟練度対応)
	all_anti_air = 0
	for n, 0, target_unit_count
		for w, 0, target_weapon_count(n)
			wid = target_weapon_id(n, w)
			if(wid == -1) :_continue
			wtype = weapon_type(wid)
			switch(wtype)
			case "艦上攻撃機"
			case "艦上戦闘機"
			case "艦上爆撃機"
			case "水上爆撃機"
			case "多用途水上機/水上戦闘機"
				;対空と搭載数に基づいた制空値
				all_anti_air += int(sqrt(target_weapon_airs(n, w)) * weapon_antiair(wid))
				;艦載機熟練度ボーナス
				wlevel = target_weapon_level(n, w)
				all_anti_air += all_bonus(wlevel)
				if(wtype == "艦上戦闘機" | wtype == "多用途水上機/水上戦闘機"){
					all_anti_air += afwf_bonus(wlevel)
				}
				if(wtype == "水上爆撃機"){
					all_anti_air+= wb_bonus(wlevel)
				}
				swbreak
			swend
		next
	next
	;索敵値を計算する(2-5式秋、33式)
	all_search_power = -0.6142467 * ((master_level - 1) / 5 + 1) * 5
	for n, 0, target_unit_count
		all_search_power += 1.6841056 * sqrt(target_unit_search(n))
		for w, 0, target_weapon_count(n)
			if(target_weapon_id(n, w) == -1) :_continue
			switch(weapon_type(target_weapon_id(n, w)))
			case "艦上爆撃機"
				all_search_power += 1.0376255 * weapon_search(target_weapon_id(n, w))
				swbreak
			case "水上爆撃機"
				all_search_power += 1.7787282 * weapon_search(target_weapon_id(n, w))
				swbreak
			case "艦上攻撃機"
				all_search_power += 1.3677954 * weapon_search(target_weapon_id(n, w))
				swbreak
			case "艦上偵察機"
				all_search_power += 1.6592780 * weapon_search(target_weapon_id(n, w))
				swbreak
			case "水上偵察機"
				all_search_power += 2.0000000 * weapon_search(target_weapon_id(n, w))
				swbreak
			case "小型電探"
				all_search_power += 1.0045358 * weapon_search(target_weapon_id(n, w))
				swbreak
			case "大型電探"
				all_search_power += 0.9906638 * weapon_search(target_weapon_id(n, w))
				swbreak
			case "探照灯"
				all_search_power += 0.9067950 * weapon_search(target_weapon_id(n, w))
				swbreak
			case "大型探照灯"
				all_search_power += 0.9067950 * weapon_search(target_weapon_id(n, w))
				swbreak
			swend
		next
	next
	all_search_power33 = -1.0 * int(0.4 * master_level)
	for n, 0, target_unit_count
		all_search_power33 += sqrt(target_unit_search(n))
		for w, 0, target_weapon_count(n)
			if(target_weapon_id(n, w) == -1) :_continue
			switch(weapon_type(target_weapon_id(n, w)))
			case "水上爆撃機"
				all_search_power33 += 1.1 * weapon_search(target_weapon_id(n, w))
				swbreak
			case "艦上攻撃機"
				all_search_power33 += 0.8 * weapon_search(target_weapon_id(n, w))
				swbreak
			case "艦上偵察機"
				all_search_power33 += 1.0 * weapon_search(target_weapon_id(n, w))
				swbreak
			case "水上偵察機"
				all_search_power33 += 1.2 * weapon_search(target_weapon_id(n, w))
				swbreak
			case "小型電探"
				all_search_power33 += 0.6 * weapon_search(target_weapon_id(n, w))
				swbreak
			case "大型電探"
				all_search_power33 += 0.6 * weapon_search(target_weapon_id(n, w))
				swbreak
			default
				all_search_power33 += 0.6 * weapon_search(target_weapon_id(n, w))
				swbreak
			swend
		next
	next
	// 結果書き出し
	fleetinfo_output = ""
	fleetinfo_output += "司令部レベル：" + master_level + "\n"
	fleetinfo_output += "制空値：" + all_anti_air + "\n"
	fleetinfo_output += "索敵値(2-5式秋)：" + strf("%.1f", all_search_power) + "\n"
	fleetinfo_output += "索敵値(33式)：" + strf("%.1f", all_search_power33) + "\n"
	fleetinfo_output += "艦娘・装備：\n"
	for n, 0, target_unit_count
		if(target_unit_id(n) == -1){
			fleetinfo_output += "不明"
		}else{
			fleetinfo_output += kammusu_name(target_unit_id(n)) + " Lv." + target_unit_level(n)
		}
		fleetinfo_output += " [装備数：" + target_weapon_count(n) + "] [素索敵：" + target_unit_search(n) + "]\n"
		for w, 0, target_weapon_count(n)
			if(target_weapon_id(n, w) == -1){
				fleetinfo_output += "　不明\n"
			}else{
				wid = target_weapon_id(n, w)
				switch(weapon_type(wid))
				case "オートジャイロ"
				case "艦上攻撃機"
				case "艦上戦闘機"
				case "艦上偵察機"
				case "艦上爆撃機"
				case "水上偵察機"
				case "水上爆撃機"
				case "多用途水上機/水上戦闘機"
				case "対潜哨戒機"
				case "大型飛行艇"
					fleetinfo_output += "　[" + target_weapon_airs(n, w) + "] " + weapon_name(wid)
					if(target_weapon_level(n, w) != 0) :fleetinfo_output += " ☆" + target_weapon_level(n, w)
					fleetinfo_output += "\n"
					swbreak
				default
					fleetinfo_output += weapon_name(wid) + "\n"
					swbreak
				swend
			}
		next
	next
	// 反映
	gsel kFleetInfoWindowID
	objprm 0, fleetinfo_output
	fleetinfo_flg = TRUE
return

//*艦隊情報からデッキビルダー用の形式に変換する
//*(ダミーデータ：吹雪、しばふ砲)
*copy_deck_format
	if(fleetinfo_flg != TRUE) :return
	if(target_unit_count <= 0) :return
	sdim deck_output, 32000
	deck_output += "{\"version\":3,\"f1\":{"
	for n, 0, target_unit_count
		if(n != 0) :deck_output += ","
		deck_output += "\"s" + (n + 1) + "\":{"
		;艦娘IDが-1なら、とりあえずID=9(吹雪)にしておく
		kid = target_unit_id(n)
		if(kid < 0){
			deck_output += "\"id\":\"9\","
		}else{
			deck_output += "\"id\":\"" + kammusu_id(kid) + "\","
		}
		;他の項目
		deck_output += "\"lv\":" + limit(target_unit_level(n), 1, kMaxKammusuLevel) + ","
		deck_output += "\"luck\":-1,"
		deck_output += "\"items\":{"
		for w, 0, target_weapon_count(n)
			if(w != 0) :deck_output += ","
			deck_output += "\"i" + (w + 1) + "\":{"
			;装備IDが-1なら、とりあえずID=2(しばふ砲)にしておく
			wid = target_weapon_id(n, w)
			if(wid < 0){
				deck_output += "\"id\":2,"
			}else{
				deck_output += "\"id\":" + weapon_id(wid) + ","
			}
			deck_output += "\"rf\":" + target_weapon_level(n, w)
			deck_output += "}"
		next
		deck_output += "}}"
	next
	deck_output += "}}"
	;クリップボードにコピー
	ClipB_SetText deck_output
return
