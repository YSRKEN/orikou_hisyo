/* 艦隊情報に関するイベント処理 */

//*艦隊情報画面を表示
*show_fleetinfo_window
	gsel kFleetInfoWindowID, 2
	show_window(kFleetInfoWindowID) = TRUE
return

//*艦隊情報を計算して出力する
*get_fleetinfo
	if(scene_fleet_offset == -1) :return
	// 情報読み取り
	master_level = GetMasterLevel()		;司令部レベル
	target_unit_count = unit_count(fleetinfo_number_index)	;艦隊の艦娘数
	dim target_unit_id, target_unit_count	;艦娘のID
	dim target_unit_level, target_unit_count	;艦娘の練度
	dim target_unit_search, target_unit_count	;艦娘の索敵値
	dim target_weapon_id, target_unit_count, kWeapons	;装備のID
	dim target_weapon_airs, target_unit_count, kWeapons	;装備の搭載数
	dim target_weapon_level_v, target_unit_count, kWeapons	;装備の熟練度(Veteran)
	dim target_weapon_level_g, target_unit_count, kWeapons	;装備の改修度(Grade)
	dim target_weapon_count, target_unit_count	;所持装備数
	for n, 0, target_unit_count
		// 艦娘情報
		;艦娘のマークを読み取り、判定を行う
		gsel GetUnitID(fleetinfo_number_index, n)
		sdim kammusu_mark_temp, kKammusuMarkLen
		for k, 0, kKammusuMarkLen
			pget 339 + k, 122
			if(ginfo_r < 210) :kammusu_mark_temp += "b" :else :kammusu_mark_temp += "w"
		next
		target_unit_id(n) = -1
		for k, 0, kammusu_data_size
			if(kammusu_mark(k) == kammusu_mark_temp){
				target_unit_id(n) = kammusu_id(k)
				_break
			}
		next
		kammusu_name_temp = GetNameFromKammusuCode(target_unit_id(n))
		fleetinfo_output = ""
		;艦娘名についての特殊補正
		if(target_unit_id(n) != -1){
			// (Graf Zeppelin)
			if(instr(kammusu_name_temp,,"Graf") != -1){
				gsel GetUnitID(fleetinfo_number_index, n)
				if(NearColor(546, 174 + 33 * 3, 27, 173, 176)){
					;Graf Zeppelin改
					target_unit_id(n) = GetCodeFromKammusuName("Graf Zeppelin改")
				}else{
					;Graf Zeppelin
					target_unit_id(n) = GetCodeFromKammusuName("Graf Zeppelin")
				}
			}
			// (Гангут)
			if(instr(kammusu_name_temp,,"Гангут") != -1){
				gsel GetUnitID(fleetinfo_number_index, n)
				if(NearColor(692, 257, 192, 122, 91)){
					;Гангут два
					target_unit_id(n) = GetCodeFromKammusuName("Гангут два")
				}else{
					;Гангут
					target_unit_id(n) = GetCodeFromKammusuName("Гангут")
				}
			}
			// (Commandant Teste)
			if(instr(kammusu_name_temp,,"Commandant") != -1){
				gsel GetUnitID(fleetinfo_number_index, n)
				if(NearColor(546, 174 + 33 * 3, 27, 173, 176)){
					;Commandant Teste改
					target_unit_id(n) = GetCodeFromKammusuName("Commandant Teste改")
				}else{
					;Commandant Teste
					target_unit_id(n) = GetCodeFromKammusuName("Commandant Teste")
				}
			}
			// (Saratoga Mk.II)
			if(instr(kammusu_name_temp,,"Saratoga Mk.II") != -1){
				gsel GetUnitID(fleetinfo_number_index, n)
				if(NearColor(586,130,244,244,244)){
					;Saratoga Mk.II Mod.2
					target_unit_id(n) = GetCodeFromKammusuName("Saratoga Mk.II")
				}else{
					;Saratoga Mk.II
					target_unit_id(n) = GetCodeFromKammusuName("Saratoga Mk.II Mod.2")
				}
			}
		}
		;艦娘の練度を取得する
		gsel GetUnitID(fleetinfo_number_index, n)
		target_unit_level(n) = 1
		if(NearColor(472, 119, 29, 189, 192) == FALSE){
			;そもそも練度が読み取れない状態＝光っている＝Lv99である
			target_unit_level(n) = 99
		}else{
			dim level_num, 3
			ReadNumber level_num, 3, unit_level_pos_x, 112, 10, 15, 170, FALSE
			if(level_num(0) == 9){
				target_unit_level(n) = 99	;Lv99用の特別な処理なんだけど、f**kな仕様すぎる……
			}else{
				if(level_num(1) == 10){
					target_unit_level(n) = limit(level_num(2), 1, 9)
				}else{
					if(level_num(0) == 10){
						target_unit_level(n) = limit(level_num(1), 1, 9) * 10 + limit(level_num(2), 0, 9)
					}else{
						target_unit_level(n) = 100 + limit(level_num(1), 0, 5) * 10 + limit(level_num(2), 0, 9)
					}
				}
			}
			target_unit_level(n) = limit(target_unit_level(n), 1, kMaxKammusuLevel)
		}
		;索敵値を読み取る
		gsel GetUnitID(fleetinfo_number_index, n)
		dim search_num, 3
		ReadNumber search_num, 3, search_pos_x, 423, 7, 12, 170, FALSE
		target_unit_search(n) = limit(search_num(2), 0, 9)
		if(search_num(1) != 10) :target_unit_search(n) += limit(search_num(1), 1, 9) * 10
		if(search_num(0) != 10) :target_unit_search(n) += limit(search_num(0), 1, 9) * 100
		// 装備情報
		for w, 0, kWeapons
			target_weapon_id(n, w) = -1
			gsel GetUnitID(fleetinfo_number_index, n)
			;装備欄なし、および未装備の場合はそれ以降読み込まない
			if(NearColor(546, 174 + 33 * w, 27, 173, 176) == FALSE){
				_break
			}
			;装備のマークを読み取り、判定を行う
			sdim weapon_mark_temp, kWeaponMarkLen
			for k, 0, kWeaponMarkLen
				pget 379 + k, 176 + 33 * w
				if(ginfo_r < 210) :weapon_mark_temp += "b" :else :weapon_mark_temp += "w"
			next
			for k, 0, weapon_data_size
				if(weapon_mark(k) == weapon_mark_temp){
					//if(n == 1) :assert :logmes weapon_mark_temp :logmes "" + k : logmes weapon_id(k)
					target_weapon_id(n, w) = weapon_id(k)
					_break
				}
			next
			target_weapon_name_temp = GetNameFromWeaponCode(target_weapon_id(n, w))
			if(n == 1) :logmes target_weapon_name_temp
			;装備名についての特殊補正
			if(target_weapon_id(n, w) != -1){
				;(381mm/50 三連装砲)
				if(instr(target_weapon_name_temp,,"381mm/50 三連装砲") != -1){
					gsel GetUnitID(fleetinfo_number_index, n)
					if(NearColor(515, 171 + 33 * w, 189, 177, 163)){
						;381mm/50 三連装砲改
						target_weapon_id(n, w) = GetCodeFromWeaponName("381mm/50 三連装砲改")
					}else{
						;381mm/50 三連装砲
						target_weapon_id(n, w) = GetCodeFromWeaponName("381mm/50 三連装砲")
					}
				}
				;(25mm三連装機銃)
				if(instr(target_weapon_name_temp,,"25mm三連装機銃") != -1){
					gsel GetUnitID(fleetinfo_number_index, n)
					if(NearColor(496, 176 + 33 * w, 92, 87, 80)){
						;25mm三連装機銃 集中配備
						target_weapon_id(n, w) = GetCodeFromWeaponName("25mm三連装機銃 集中配備")
					}else{
						;25mm三連装機銃
						target_weapon_id(n, w) = GetCodeFromWeaponName("25mm三連装機銃")
					}
				}
				;(12.7cm連装高角砲)
				if(instr(target_weapon_name_temp,,"12.7cm連装高角砲") != -1){
					gsel GetUnitID(fleetinfo_number_index, n)
					if(NearColor(497, 177 + 33 * w, 92, 86, 80)){
						;12.7cm連装高角砲(後期型)
						target_weapon_id(n, w) = GetCodeFromWeaponName("12.7cm連装高角砲(後期型)")
					}else{
						;12.7cm連装高角砲
						target_weapon_id(n, w) = GetCodeFromWeaponName("12.7cm連装高角砲")
					}
				}
				;(16inch三連装砲 Mk.7)
				if(instr(target_weapon_name_temp,,"16inch三連装砲 Mk.7") != -1){
					gsel GetUnitID(fleetinfo_number_index, n)
					if(NearColor(516, 176 + 33 * w, 205, 192, 177)){
						;16inch三連装砲 Mk.7+GFCS
						target_weapon_id(n, w) = GetCodeFromWeaponName("16inch三連装砲 Mk.7+GFCS")
					}else{
						;16inch三連装砲 Mk.7
						target_weapon_id(n, w) = GetCodeFromWeaponName("16inch三連装砲 Mk.7")
					}
				}
			}
			;搭載数を読み取る
			if(target_weapon_id(n, w) != -1){
				temp_type = GetTypeFromWeaponCode(target_weapon_id(n, w))
				switch(temp_type)
				case "オートジャイロ"
				case "艦上攻撃機"
				case "艦上戦闘機"
				case "艦上偵察機"
				case "艦上爆撃機"
				case "水上偵察機"
				case "水上爆撃機"
				case "多用途水上機/水上戦闘機"
				case "水上戦闘機"
				case "対潜哨戒機"
				case "大型飛行艇"
				case "噴式戦闘爆撃機"
					dim airs_num, 2
					ReadNumber airs_num, 2, airs_pos_x, 169 + w * 33, 8, 12, 170, FALSE
					if(airs_num(1) != 10){
						target_weapon_airs(n, w) = limit(airs_num(1), 0, 9)
						if(airs_num(0) != 10) :target_weapon_airs(n, w) += limit(airs_num(0), 1, 9) * 10
					}
				swend
			}
			;装備数を更新する
			target_weapon_count(n)++
		next
	next
//	assert
	// 装備から、索敵値に補正を入れる
	for n, 0, target_unit_count
		for w, 0, target_weapon_count(n)
			if(target_weapon_id(n, w) == -1) :_continue
			target_unit_search(n) -= GetSearchValueFromWeaponCode(target_weapon_id(n, w))
		next
	next
//	assert
	// 艦載機熟練度を読み取る
	for n, 0, target_unit_count
		gsel GetUnitID(fleetinfo_number_index, n)
		for w, 0, target_weapon_count(n)
			id = target_weapon_id(n, w)
			if(id == -1) :_continue
			temp_type = GetTypeFromWeaponCode(target_weapon_id(n, w))
			switch(temp_type)
			case "艦上攻撃機"
			case "艦上戦闘機"
			case "艦上爆撃機"
			case "艦上偵察機"
			case "水上爆撃機"
			case "水上偵察機"
			case "多用途水上機/水上戦闘機"
			case "水上戦闘機"
			case "噴式戦闘爆撃機"
				// Max(>>)
				if(NearColor(508, 176 + 33 * w, 214, 159, 21)){
					if(NearColor(502, 176 + 33 * w, 214, 159, 20)){
						target_weapon_level_v(n, w) = 7
						swbreak
					}
				}
				// Level 4・5・6(/・//・///)
				if(NearColor(507, 176 + 33 * w, 213, 156, 8)){
					if(NearColor(503, 176 + 33 * w, 213, 156, 8)){
						if(NearColor(499, 176 + 33 * w, 213, 156, 8)){
							target_weapon_level_v(n, w) = 6
						}else{
							target_weapon_level_v(n, w) = 5
						}
					}else{
						target_weapon_level_v(n, w) = 4
					}
					swbreak
				}
				// Level 1・2・3(|・||・|||)
				if(NearColor(508, 176 + 33 * w, 154, 181, 208)){
					if(NearColor(504, 176 + 33 * w, 154, 181, 208)){
						if(NearColor(500, 176 + 33 * w, 154, 181, 208)){
							target_weapon_level_v(n, w) = 3
						}else{
							target_weapon_level_v(n, w) = 2
						}
					}else{
						target_weapon_level_v(n, w) = 1
					}
					swbreak
				}
				swbreak
			swend
		next
	next
//	assert
	// 装備改修度を読み取る
	for n, 0, target_unit_count
		gsel GetUnitID(fleetinfo_number_index, n)
		for w, 0, target_weapon_count(n)
			id = target_weapon_id(n, w)
			if(id == -1) :_continue
			// ★MAX用
			if(NearColor(525, 175 + 33 * w, 255, 255, 255)){
				target_weapon_level_g(n, w) = 10
			}
			// ★1〜9用
			if(NearColor(517, 175 + 33 * w, 69, 169, 165)){
				// ★9
				if(NearColor(536, 170 + 33 * w, 124, 185, 177)){
					if(NearColor(533, 180 + 33 * w, 159, 195, 185)){
						target_weapon_level_g(n, w) = 9
						_continue
					}
				}
				// ★8
				if(NearColor(538, 178 + 33 * w, 106, 179, 173)){
					if(NearColor(538, 172 + 33 * w, 111, 181, 174)){
						target_weapon_level_g(n, w) = 8
						_continue
					}
				}
				// ★7
				if(NearColor(533, 171 + 33 * w, 114, 182, 175)){
					if(NearColor(538, 170 + 33 * w, 131, 186, 178)){
						target_weapon_level_g(n, w) = 7
						_continue
					}
				}
				// ★6
				if(NearColor(534, 173 + 33 * w, 70, 170, 166)){
					if(NearColor(537, 170 + 33 * w, 129, 186, 178)){
						target_weapon_level_g(n, w) = 6
						_continue
					}
				}
				// ★5
				if(NearColor(538, 170 + 33 * w, 137, 188, 179)){
					if(NearColor(536, 180 + 33 * w, 156, 194, 184)){
						target_weapon_level_g(n, w) = 5
						_continue
					}
				}
				// ★4
				if(NearColor(536, 176 + 33 * w, 72, 170, 166)){
					if(NearColor(533, 176 + 33 * w, 136, 188, 180)){
						target_weapon_level_g(n, w) = 4
						_continue
					}
				}
				// ★3
				if(NearColor(533, 179 + 33 * w, 120, 184, 176)){
					if(NearColor(535, 175 + 33 * w, 135, 188, 180)){
						target_weapon_level_g(n, w) = 3
						_continue
					}
				}
				// ★2
				if(NearColor(534, 179 + 33 * w, 79, 172, 168)){
					if(NearColor(539, 177 + 33 * w, 136, 188, 179)){
						target_weapon_level_g(n, w) = 2
						_continue
					}
				}
				// ★1
				if(NearColor(536, 179 + 33 * w, 94, 176, 171)){
					if(NearColor(535, 171 + 33 * w, 70, 170, 166)){
						target_weapon_level_g(n, w) = 1
						_continue
					}
				}
			}
		next
	next
//	assert
	// 制空値を計算する(艦載機熟練度対応)
	all_anti_air = 0
	for n, 0, target_unit_count
		for w, 0, target_weapon_count(n)
			wid = target_weapon_id(n, w)
			if(wid == -1) :_continue
			wtype = GetTypeFromWeaponCode(target_weapon_id(n, w))
			wair = GetAntiAirFromWeaponCode(target_weapon_id(n, w))
			switch(wtype)
			case "艦上攻撃機"
			case "艦上戦闘機"
			case "艦上爆撃機"
			case "水上爆撃機"
			case "多用途水上機/水上戦闘機"
			case "水上戦闘機"
			case "噴式戦闘爆撃機"
				;対空と搭載数に基づいた制空値
				if(wtype == "艦上戦闘機" || wtype == "多用途水上機/水上戦闘機" || wtype == "水上戦闘機"){
					coeff = 0.2
				}else{
					coeff = 0.25
				}
				all_anti_air += int(sqrt(target_weapon_airs(n, w)) * (coeff * target_weapon_level_g(n, w) + 1.0 * wair))
				;艦載機熟練度ボーナス
				wlevel = target_weapon_level_v(n, w)
				all_anti_air += all_bonus(wlevel)
				if(wtype == "艦上戦闘機" | wtype == "多用途水上機/水上戦闘機" || wtype == "水上戦闘機"){
					all_anti_air += afwf_bonus(wlevel)
				}
				if(wtype == "水上爆撃機"){
					all_anti_air+= wb_bonus(wlevel)
				}
				swbreak
			swend
		next
	next
//	assert
	// 索敵値を計算する(2-5式秋、33式)
	;2-5式秋
	if(master_level \ 5 == 0){
		all_search_power = -0.6142467 * master_level
	}else{
		all_search_power = -0.6142467 * ((master_level / 5) + 1) * 5
	}
	for n, 0, target_unit_count
		all_search_power += 1.6841056 * sqrt(target_unit_search(n))
		for w, 0, target_weapon_count(n)
			if(target_weapon_id(n, w) == -1) :_continue
			wtype = GetTypeFromWeaponCode(target_weapon_id(n, w))
			wsearch = GetSearchValueFromWeaponCode(target_weapon_id(n, w))
			switch(wtype)
			case "艦上爆撃機"
				all_search_power += 1.0376255 * wsearch
				swbreak
			case "艦上攻撃機"
				all_search_power += 1.3677954 * wsearch
				swbreak
			case "艦上偵察機"
				all_search_power += 1.6592780 * wsearch
				swbreak
			case "水上偵察機"
				all_search_power += 2.0000000 * wsearch
				swbreak
			case "水上爆撃機"
				all_search_power += 1.7787282 * wsearch
				swbreak
			case "小型電探"
				all_search_power += 1.0045358 * wsearch
				swbreak
			case "大型電探"
				all_search_power += 0.9906638 * wsearch
				swbreak
			case "探照灯"
				all_search_power += 0.9067950 * wsearch
				swbreak
			case "大型探照灯"
				all_search_power += 0.9067950 * wsearch
				swbreak
			swend
		next
	next
//	assert
	;33式
	if(4 * master_level \ 10 != 0){
		;ceil関数の実装が面倒だったので場合分け
		all_search_power33 = -1.0 * (int(0.4 * master_level) + 1)
	}else{
		all_search_power33 = -1.0 * int(0.4 * master_level)
	}
	for n, 0, target_unit_count
		all_search_power33 += sqrt(target_unit_search(n))
		for w, 0, target_weapon_count(n)
			if(target_weapon_id(n, w) == -1) :_continue
			search_value = GetSearchValueFromWeaponCode(target_weapon_id(n, w))
			wtype = GetTypeFromWeaponCode(target_weapon_id(n, w))
			switch(wtype)
			case "艦上戦闘機"
			case "艦上爆撃機"
				all_search_power33 += 0.6 * search_value
				swbreak
			case "艦上攻撃機"
				all_search_power33 += 0.8 * search_value
				swbreak
			case "艦上偵察機"
				all_search_power33 += 1.0 * search_value
				swbreak
			case "水上偵察機"
				all_search_power33 += 1.2 * (1.0 * search_value + 1.2 * sqrt(target_weapon_level_g(n, w)))
				swbreak
			case "水上爆撃機"
				all_search_power33 += 1.1 * search_value
				swbreak
			case "小型電探"
			case "大型電探"
				all_search_power33 += 0.6 * (1.0 * search_value + 1.25 * sqrt(target_weapon_level_g(n, w)))
				swbreak
			case "対潜哨戒機"
			case "航空要員"
			case "水上艦要員"
			case "大型ソナー"
			case "大型飛行艇"
			case "大型探照灯"
			case "多用途水上機/水上戦闘機"
			case "水上戦闘機"
				all_search_power33 += 0.6 * search_value
				swbreak
			default
			swend
		next
	next
	all_search_power33 += 2.0 * (6 - target_unit_count)
	// TP(輸送物資量)を計算する
	tp_value_s = 0.0 :tp_value_a = 0.0
	for n, 0, target_unit_count
		kid = target_unit_id(n)
		if(kid < 0) :_continue
		ktype = GetTypeFromKammusuCode(kid)
		switch(ktype)
		case "駆逐艦"
			tp_value_s += 5.0
			tp_value_a += 3.5
			swbreak
		case "軽巡洋艦"
			tp_value_s += 2.0
			tp_value_a += 1.4
			// 鬼怒改二のみの特殊補正
			// (大発を内蔵していることによる)
			kname = GetNameFromKammusuCode(kid)
			if(kname == "鬼怒改二"){
				tp_value_s += 8.0
				tp_value_a += 5.6
			}
			swbreak
		case "航空巡洋艦"
			tp_value_s += 4.0
			tp_value_a += 2.8
			swbreak
		case "航空戦艦"
			tp_value_s += 7.0
			tp_value_a += 4.9
			swbreak
		case "水上機母艦"
			tp_value_s += 9.0
			tp_value_a += 6.3
			swbreak
		case "揚陸艦"
			tp_value_s += 12.0
			tp_value_a += 8.4
			swbreak
		case "潜水母艦"
			tp_value_s += 7.0
			tp_value_a += 4.9
			swbreak
		case "練習巡洋艦"
			tp_value_s += 6.0
			tp_value_a += 4.2
			swbreak
		case "補給艦"
			tp_value_s += 15.0
			tp_value_a += 10.5
			swbreak
		swend
		for w, 0, target_weapon_count(n)
			wid = target_weapon_id(n, w)
			if(wid == -1) :_continue
			wname = GetNameFromWeaponCode(wid)
			switch(wname)
			case "ドラム缶(輸送用)"
				tp_value_s += 5.0
				tp_value_a += 3.5
				swbreak
			case "大発動艇"
			case "特大発動艇"
			case "大発動艇(八九式中戦車＆陸戦隊)"
				tp_value_s += 8.0
				tp_value_a += 5.6
				swbreak
			case "特二式内火艇"
				tp_value_s += 2.0
				tp_value_a += 1.4
				swbreak
			case "戦闘糧食"
			case "秋刀魚の缶詰"
				tp_value_s += 1.0
				tp_value_a += 0.7
				swbreak
			swend
			_continue
		next
	next
	// 結果書き出し
	fleetinfo_output += "司令部レベル：" + master_level + "\n"
	fleetinfo_output += "制空値：" + all_anti_air + "\n"
	fleetinfo_output += "索敵値(2-5式秋)：" + strf("%.1f", all_search_power) + "\n"
	fleetinfo_output += "索敵値(33式)：" + strf("%.1f", all_search_power33) + "\n"
	fleetinfo_output += "輸送物資量(TP)：" + strf("%.1f(S),%.1f(A)", tp_value_s, tp_value_a) + "\n"
	fleetinfo_output += "艦娘・装備：\n"
	for n, 0, target_unit_count
		if(target_unit_id(n) == -1){
			fleetinfo_output += "不明"
		}else{
			fleetinfo_output += GetNameFromKammusuCode(target_unit_id(n)) + " Lv." + target_unit_level(n)
		}
		fleetinfo_output += " [装備数：" + target_weapon_count(n) + "] [素索敵：" + target_unit_search(n) + "]\n"
		for w, 0, target_weapon_count(n)
			if(target_weapon_id(n, w) == -1){
				fleetinfo_output += "　不明\n"
			}else{
				wid = target_weapon_id(n, w)
				logmes GetNameFromWeaponCode(wid)
				logmes GetTypeFromWeaponCode(wid)
				wtype = GetTypeFromWeaponCode(wid)
				switch(wtype)
				case "オートジャイロ"
				case "艦上攻撃機"
				case "艦上戦闘機"
				case "艦上偵察機"
				case "艦上爆撃機"
				case "水上偵察機"
				case "水上爆撃機"
				case "多用途水上機/水上戦闘機"
				case "水上戦闘機"
				case "対潜哨戒機"
				case "大型飛行艇"
				case "噴式戦闘爆撃機"
					fleetinfo_output += "　[" + target_weapon_airs(n, w) + "] " + GetNameFromWeaponCode(wid)
					swbreak
				default
					fleetinfo_output += "　" + GetNameFromWeaponCode(wid)
					swbreak
				swend
				if(target_weapon_level_v(n, w) != 0) :fleetinfo_output += " ☆" + target_weapon_level_v(n, w)
				if(target_weapon_level_g(n, w) != 0) :fleetinfo_output += " ★" + target_weapon_level_g(n, w)
				fleetinfo_output += "\n"
			}
		next
	next
	// 反映
	gsel kFleetInfoWindowID
	objprm 0, fleetinfo_output
	fleetinfo_flg = TRUE
return

//*艦隊情報からデッキビルダー用の形式に変換する
//*2016/7/2にデータ構造に変更があった
//*(ダミーデータ：吹雪)
*copy_deck_format
	if(fleetinfo_flg != TRUE) :return
	if(target_unit_count <= 0) :return
	sdim deck_output, 32000
	deck_output += "{\"version\":4,\"f1\":{"
	for n, 0, target_unit_count
		if(n != 0) :deck_output += ","
		deck_output += "\"s" + (n + 1) + "\":{"
		;艦娘IDが-1なら、とりあえずID=9(吹雪)にしておく
		kid = target_unit_id(n)
		if(kid < 0){
			deck_output += "\"id\":\"9\","
		}else{
			deck_output += "\"id\":\"" + kid + "\","
		}
		;他の項目
		deck_output += "\"lv\":" + limit(target_unit_level(n), 1, kMaxKammusuLevel) + ","
		deck_output += "\"luck\":-1,"
		deck_output += "\"items\":{"
		for w, 0, target_weapon_count(n)
			;装備IDが-1ならとりあえず飛ばす
			wid = target_weapon_id(n, w)
			if(wid < 0) :_continue
			;末尾でないならカンマ付与
			if(w != 0) :deck_output += ","
			;装備ID
			deck_output += "\"i" + (w + 1) + "\":{"
			deck_output += "\"id\":" + wid + ","
			;装備改修度
			deck_output += "\"rf\":" + target_weapon_level_g(n, w) + ","
			;艦載機熟練度
			deck_output += "\"mas\":" + target_weapon_level_v(n, w) + "}"
		next
		deck_output += "}}"
	next
	deck_output += "}}"
	;クリップボードにコピー
	ClipB_SetText deck_output
return
