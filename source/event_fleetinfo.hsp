/* 艦隊情報に関するイベント処理 */

*show_fleetinfo_window
	gsel kFleetInfoWindowID, 2
	show_window(kFleetInfoWindowID) = TRUE
return

*get_fleetinfo
	// 情報読み取り
	target_unit_count = unit_count(0)
	dim target_unit_id, target_unit_count
	dim target_weapon_id, target_unit_count, kWeapons
	dim target_weapon_count, target_unit_count
	for n, 0, target_unit_count
		gsel GetUnitID(0, n)
		// 艦娘情報
		sdim kammusu_mark_temp, kKammusuMarkLen
		for k, 0, kKammusuMarkLen
			pget 339 + k, 122
			if(ginfo_r < 210) :kammusu_mark_temp += "b" :else :kammusu_mark_temp += "w"
		next
		target_unit_id(n) = -1
		for k, 0, kammusu_data_size
			if(kammusu_mark(k) == kammusu_mark_temp){
				target_unit_id(n) = k
				_break
			}
		next
		// 装備情報
		for w, 0, kWeapons
			target_weapon_id(n, w) = -1
			if(NearColor(546, 174 + 33 * w, 27, 173, 176) == FALSE){
				_break
			}
			if(NearColor(362, 175 + 33 * w, 39, 64, 46) && NearColor(361, 173 + 33 * w, 41, 63, 46)){
				_break
			}
			sdim weapon_mark_temp, kWeaponMarkLen
			for k, 0, kWeaponMarkLen
				pget 379 + k, 176 + 33 * w
				if(ginfo_r < 210) :weapon_mark_temp += "b" :else :weapon_mark_temp += "w"
			next
			for k, 0, weapon_data_size
				if(weapon_mark(k) == weapon_mark_temp){
					target_weapon_id(n, w) = k
					_break
				}
			next
			target_weapon_count(n)++
		next
	next
	// 結果書き出し
	fleetinfo_output = "艦娘・装備：\n"
	for n, 0, target_unit_count
		if(target_unit_id(n) == -1){
			fleetinfo_output += "不明"
		}else{
			fleetinfo_output += kammusu_name(target_unit_id(n))
		}
		fleetinfo_output += " (装備数：" + target_weapon_count(n) + ")\n"
		for w, 0, target_weapon_count(n)
			if(target_weapon_id(n, w) == -1){
				fleetinfo_output += "　不明\n"
			}else{
				fleetinfo_output += "　" + weapon_name(target_weapon_id(n, w)) + "\n"
			}
		next
	next
	// 反映
	gsel kFleetInfoWindowID
	objprm 0, fleetinfo_output
return
